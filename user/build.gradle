if (rootProject.ext.isRunAlone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        if (rootProject.ext.isRunAlone) {
            applicationId "com.zch.user"
        }

        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        if (!rootProject.ext.isRunAlone) {
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [moduleName: project.getName()]
                }
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (rootProject.ext.isRunAlone) {
                manifest.srcFile 'src/main/develop/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    annotationProcessor rootProject.ext.dependencies.butterknife_compiler
    if (!rootProject.ext.isRunAlone) {
        annotationProcessor rootProject.ext.dependencies.arouter_compiler
    }

    api project(':bizzlib')
}
